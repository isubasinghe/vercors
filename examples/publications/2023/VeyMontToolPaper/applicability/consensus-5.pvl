class Process {
  int n, f, fault;
  int v, v1, v2, v3, v4, v5, tiebreaker;

  inline resource class_invariant() =
    (fault == 0 || fault == 1) &&
    (v == 0 || v == 1) &&
    (v1 == 0 || v1 == 1) &&
    (v2 == 0 || v2 == 1) &&
    (v3 == 0 || v3 == 1) &&
    (v4 == 0 || v4 == 1) &&
    (v5 == 0 || v5 == 1) &&
    (tiebreaker == 0 || tiebreaker == 1);

  inline resource constants() =
    \old(n) == n &&
    \old(f) == f &&
    \old(fault) == fault;

  requires v == 0 || v == 1;
  requires fault == 0 || fault == 1;
  ensures this.n == n;
  ensures this.f == f;
  ensures this.fault == fault;
  ensures this.v == v;
  ensures class_invariant();
  Process(int n, int f, int fault, int v) {
    this.n = n;
    this.f = f;
    this.fault = fault;
    this.v = v;
    this.v1 = 0;
    this.v2 = 0;
    this.v3 = 0;
    this.v4 = 0;
    this.v5 = 0;
    this.tiebreaker = 0;
  }

  requires class_invariant();
  ensures \result == n - (v1 + v2 + v3 + v4 + v5);
  pure int zeros() {
    return n - ones();
  }

  requires class_invariant();
  ensures \result == v1 + v2 + v3 + v4 + v5;
  pure int ones() {
    return v1 + v2 + v3 + v4 + v5;
  }

  requires class_invariant();
  ensures (zeros() >= ones() ==> \result == 0);
  ensures (zeros() < ones() ==> \result == 1);
  pure int majority() {
    return zeros() < ones() ? 1 : 0;
  }

  requires class_invariant();
  ensures zeros() >= ones() ==> \result == zeros();
  ensures zeros() <  ones() ==> \result == ones();
  pure int mult() {
    return zeros() < ones() ? ones() : zeros();
  }

  context class_invariant();
  ensures constants();
  ensures \old(v) == v;
  ensures \old(v1) == v1;
  ensures \old(v2) == v2;
  ensures \old(v3) == v3;
  ensures \old(v4) == v4;
  ensures \old(v5) == v5;
  ensures \old(tiebreaker) == tiebreaker;
  ensures fault == 0 ==> \result == v;
  ensures fault == 1 ==> \result == 0 || \result == 1;
  int getValueOrFail();

  context class_invariant();
  ensures constants();
  ensures \old(v) == v;
  ensures \old(v1) == v1;
  ensures \old(v2) == v2;
  ensures \old(v3) == v3;
  ensures \old(v4) == v4;
  ensures \old(v5) == v5;
  ensures \old(tiebreaker) == tiebreaker;
  ensures fault == 0 ==> \result == majority();
  ensures fault == 1 ==> \result == 0 || \result == 1;
  int getMajorityOrFail();

  context class_invariant();
  ensures constants();
  ensures \old(v1) == v1;
  ensures \old(v2) == v2;
  ensures \old(v3) == v3;
  ensures \old(v4) == v4;
  ensures \old(v5) == v5;
  ensures \old(tiebreaker) == tiebreaker;
  ensures fault == 0 && mult() >= (n + 1) / 2 + f ==> v == majority();
  ensures fault == 0 && mult() <  (n + 1) / 2 + f ==> v == tiebreaker;
  ensures fault == 1 ==> v == 0 || v == 1;
  void setValueOrFail();
}

class SeqProgram {
  Process p1, p2, p3, p4, p5;

  inline resource consistency() =
    p1.n == p2.n && p2.n == p3.n && p3.n == p4.n && p4.n == p5.n && p5.n == p1.n &&
    p1.f == p2.f && p2.f == p3.f && p3.f == p4.f && p4.f == p5.f && p5.f == p1.f;

  requires n == 5;
//*
  requires f == 1;
/*/
  requires f == 2;
//*/
  requires fault1 == 0 || fault1 == 1;
  requires fault2 == 0 || fault2 == 1;
  requires fault3 == 0 || fault3 == 1;
  requires fault4 == 0 || fault4 == 1;
  requires fault5 == 0 || fault5 == 1;
  requires v1 == 0 || v1 == 1;
  requires v2 == 0 || v2 == 1;
  requires v3 == 0 || v3 == 1;
  requires v4 == 0 || v4 == 1;
  requires v5 == 0 || v5 == 1;
  requires fault1 + fault2 + fault3 + fault4 + fault5 <= f;
  ensures p1.class_invariant();
  ensures p2.class_invariant();
  ensures p3.class_invariant();
  ensures p4.class_invariant();
  ensures p5.class_invariant();
  ensures consistency();
//*
  ensures p1.fault + p2.fault + p3.fault + p4.fault + p5.fault <= p1.f && p1.f == 1 && p1.n == 5;
/*/
  ensures p1.fault + p2.fault + p3.fault + p4.fault + p5.fault <= p1.f && p1.f == 2 && p1.n == 5;
//*/
  SeqProgram(int n, int f,
             int fault1, int v1,
             int fault2, int v2,
             int fault3, int v3,
             int fault4, int v4,
             int fault5, int v5) {
    p1 = new Process(n, f, fault1, v1);
    p2 = new Process(n, f, fault2, v2);
    p3 = new Process(n, f, fault3, v3);
    p4 = new Process(n, f, fault4, v4);
    p5 = new Process(n, f, fault5, v5);
  }

  requires p1.class_invariant();
  requires p2.class_invariant();
  requires p3.class_invariant();
  requires p4.class_invariant();
  requires p5.class_invariant();
  requires consistency();
//*
  requires p1.fault + p2.fault + p3.fault + p4.fault + p5.fault <= p1.f && p1.f == 1 && p1.n == 5;
/*/
  requires p1.fault + p2.fault + p3.fault + p4.fault + p5.fault <= p1.f && p1.f == 2 && p1.n == 5;
//*/
  ensures ((p1.fault == 0 ==> p1.v == 0) &&
           (p2.fault == 0 ==> p2.v == 0) &&
           (p3.fault == 0 ==> p3.v == 0) &&
           (p4.fault == 0 ==> p4.v == 0) &&
           (p5.fault == 0 ==> p5.v == 0)) ||
          ((p1.fault == 0 ==> p1.v == 1) &&
           (p2.fault == 0 ==> p2.v == 1) &&
           (p3.fault == 0 ==> p3.v == 1) &&
           (p4.fault == 0 ==> p4.v == 1) &&
           (p5.fault == 0 ==> p5.v == 1));
  void run() {

    // Phase 1 (p1 is "king")
    p1.v1 = p1.getValueOrFail(); p2.v1 = p1.getValueOrFail(); p3.v1 = p1.getValueOrFail(); p4.v1 = p1.getValueOrFail(); p5.v1 = p1.getValueOrFail();
    p1.v2 = p2.getValueOrFail(); p2.v2 = p2.getValueOrFail(); p3.v2 = p2.getValueOrFail(); p4.v2 = p2.getValueOrFail(); p5.v2 = p2.getValueOrFail();
    p1.v3 = p3.getValueOrFail(); p2.v3 = p3.getValueOrFail(); p3.v3 = p3.getValueOrFail(); p4.v3 = p3.getValueOrFail(); p5.v3 = p3.getValueOrFail();
    p1.v4 = p4.getValueOrFail(); p2.v4 = p4.getValueOrFail(); p3.v4 = p4.getValueOrFail(); p4.v4 = p4.getValueOrFail(); p5.v4 = p4.getValueOrFail();
    p1.v5 = p5.getValueOrFail(); p2.v5 = p5.getValueOrFail(); p3.v5 = p5.getValueOrFail(); p4.v5 = p5.getValueOrFail(); p5.v5 = p5.getValueOrFail();
    p1.tiebreaker = p1.getMajorityOrFail();
    p2.tiebreaker = p1.getMajorityOrFail();
    p3.tiebreaker = p1.getMajorityOrFail();
    p4.tiebreaker = p1.getMajorityOrFail();
    p5.tiebreaker = p1.getMajorityOrFail();
    p1.setValueOrFail();
    p2.setValueOrFail();
    p3.setValueOrFail();
    p4.setValueOrFail();
    p5.setValueOrFail();

    // Phase 2 (p2 is "king")
    p1.v1 = p1.getValueOrFail(); p2.v1 = p1.getValueOrFail(); p3.v1 = p1.getValueOrFail(); p4.v1 = p1.getValueOrFail(); p5.v1 = p1.getValueOrFail();
    p1.v2 = p2.getValueOrFail(); p2.v2 = p2.getValueOrFail(); p3.v2 = p2.getValueOrFail(); p4.v2 = p2.getValueOrFail(); p5.v2 = p2.getValueOrFail();
    p1.v3 = p3.getValueOrFail(); p2.v3 = p3.getValueOrFail(); p3.v3 = p3.getValueOrFail(); p4.v3 = p3.getValueOrFail(); p5.v3 = p3.getValueOrFail();
    p1.v4 = p4.getValueOrFail(); p2.v4 = p4.getValueOrFail(); p3.v4 = p4.getValueOrFail(); p4.v4 = p4.getValueOrFail(); p5.v4 = p4.getValueOrFail();
    p1.v5 = p5.getValueOrFail(); p2.v5 = p5.getValueOrFail(); p3.v5 = p5.getValueOrFail(); p4.v5 = p5.getValueOrFail(); p5.v5 = p5.getValueOrFail();
    p1.tiebreaker = p2.getMajorityOrFail();
    p2.tiebreaker = p2.getMajorityOrFail();
    p3.tiebreaker = p2.getMajorityOrFail();
    p4.tiebreaker = p2.getMajorityOrFail();
    p5.tiebreaker = p2.getMajorityOrFail();
    p1.setValueOrFail();
    p2.setValueOrFail();
    p3.setValueOrFail();
    p4.setValueOrFail();
    p5.setValueOrFail();
  }
}